plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
}

android {
    namespace 'io.github.xnovo3000.openweather'
    compileSdk 33
    defaultConfig {
        applicationId 'io.github.xnovo3000.openweather'
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName '1.0'
    }
    buildFeatures {
        compose true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.4.3'
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlin {
        jvmToolchain(11)
    }
}

dependencies {
    // AndroidX Base
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0'
    // Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'
    // Dagger Hilt - Navigation add-on
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    // Dagger Hilt - Work add-on
    implementation 'androidx.hilt:hilt-work:1.0.0'
    // DataStore
    implementation 'androidx.datastore:datastore:1.0.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.22.2'
    // Google Play Services
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    // ICU4J
    implementation 'com.ibm.icu:icu4j:72.1'
    // Jetpack Compose
    implementation 'androidx.compose.animation:animation:1.3.3'
    implementation 'androidx.compose.compiler:compiler:1.4.3'
    implementation 'androidx.compose.foundation:foundation:1.3.1'
    implementation 'androidx.compose.runtime:runtime:1.3.3'
    implementation 'androidx.compose.ui:ui:1.3.3'
    // Jetpack Compose - Material You
    implementation 'androidx.compose.material:material-icons-extended:1.3.1'
    implementation 'androidx.compose.material3:material3:1.0.1'
    // Jetpack Compose - Preview
    implementation 'androidx.compose.ui:ui-tooling:1.3.3'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.3.3'
    // Jetpack Compose - Google Accompanist
    implementation 'com.google.accompanist:accompanist-drawablepainter:0.28.0'
    implementation 'com.google.accompanist:accompanist-navigation-animation:0.28.0'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.28.0'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // Room
    implementation 'androidx.room:room-ktx:2.5.0'
    kapt 'androidx.room:room-compiler:2.5.0'
    // Work
    implementation 'androidx.work:work-runtime-ktx:2.8.0'
    // JDK 11 desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.2'
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}